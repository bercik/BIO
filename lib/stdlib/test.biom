@ bt_assert_eq(actual, expected, msg=none)
@ bt_assert_neq(actual, expected, msg=none)
@ bt_assert_true(bval, msg=none)
@ bt_assert_false(bval, msg=none)
@ bt_assert_error(val, msg=none)

#INCLUDE("list.biom")
#INCLUDE("iter_functions.biom")

def _call_each_function(el, obj)
   AS_GLOB(bt_fun_name, el.val)
   PRINTLN(el.val)
   CALL_BY_NAME(el.val)
end

def _print_errors_summary(el, obj)
   IF(EQ(el.val, 0), RET(none))
   AS_LOC(msg, ADD(el.key, ": ", TO_STR(el.val)))
   PRINTLN(msg)
end

def onSTART()
   @ słownik przechowujący liczbę errorów dla każdej funkcji
   AS_GLOB(bt_errors_dict, CREATE_DICT())
   @ pobieramy tablicę nazw funkcji użytkownika
   AS_LOC(funNames, GET_USER_FUNCTIONS_NAMES())
   @ tworzymy listę z nazwami funkcji zaczynającymi się od test
   AS_LOC(list, FOREACH(funNames, onEACH_ret_list_with_strings_starts_with_case_insensitive, "test"))

   @ wywołujemy wszystkie funkcje
   FOREACH(get_array_list(list), _call_each_function)

   @ wyświetlamy podsumowanie
   AS_LOC(errors_dict, GET_GLOB(bt_errors_dict))
   @ podliczamy ilość zer
   AS_LOC(zeros, COUNT_EL(errors_dict, 0))

   PRINTLN()
   PRINTLN("SUMMARY")
   PRINTLN("--------------")

   @ jeżeli ilość zer jest równa ilości funkcji to znaczy
   @ że żaden test nie zawiódł
   IF
   (
      EQ(zeros, size_of_list(list)),
      CALL
      (
         PRINTLN("All test passed without errors"),
         EXIT()
      )
   )

   PRINTLN("Some test passed with errors. Here is list of tests and number of errors:")
   FOREACH(errors_dict, _print_errors_summary)
end

def bt_assert_true(bval, msg=none)
   AS_LOC(funName, GET_GLOB(bt_fun_name))
   _init_errors_dict(funName)
   IF
   (
      NOT(bval),
      _error(funName, msg)
   )
end

def bt_assert_false(bval, msg=none)
   AS_LOC(funName, GET_GLOB(bt_fun_name))
   _init_errors_dict(funName)
   IF
   (
      bval,
      _error(funName, msg)
   )
end

def bt_assert_eq(actual, expected, msg=none)
   AS_LOC(funName, GET_GLOB(bt_fun_name))
   _init_errors_dict(funName)
   IF
   (
      NEQ(actual, expected),
      _error(funName, msg, expVal=expected, actVal=actual)
   )
end

def bt_assert_neq(actual, expected, msg=none)
   AS_LOC(funName, GET_GLOB(bt_fun_name))
   _init_errors_dict(funName)
   IF
   (
      EQ(actual, expected),
      _error(funName, msg, expVal=expected, actVal=actual)
   )
end

def bt_assert_error(val, msg=none)
   AS_LOC(funName, GET_GLOB(bt_fun_name))
   _init_errors_dict(funName)
   IF
   (
      NOT(IS_ERROR(val)),
      _error(funName, msg)
   )
end

def _error(funName, msg, expVal=none, actVal=none)
   _add_error(funName)
   PRINT_NTH_LAST_STACK_TRACE(CREATE_ERROR("", none, none), 1)
   PRINT(" failed")
   IF
   (
      AND(NOT(IS_NONE(expVal)), NOT(IS_NONE(actVal))),
      PRINT(". Expected", expVal, "got", actVal)
   )
   IF(NOT(IS_NONE(msg)), PRINTLN(ADD(": \"", msg, "\"")), PRINTLN())
end

def _init_errors_dict(funName)
   @ przypisujemy globalny słownik do zmiennej lokalnej
   AS_LOC(dict, GET_GLOB(bt_errors_dict))
   @ jeżeli klucz funName nie jest ustawiony to ustawiamy na 0
   IF(NOT(CONTAINS_KEY(dict, funName)), SET(dict, funName, 0)) 
end

def _add_error(funName)
   @ przypisujemy globalny słownik do zmiennej lokalnej
   AS_LOC(dict, GET_GLOB(bt_errors_dict))
   @ dodajemy jeden do klucza funName
   SET(dict, funName, ADD(GET(dict, funName), 1))
end
